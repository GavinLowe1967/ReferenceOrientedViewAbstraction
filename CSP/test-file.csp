-- @VA component process = FreeNode
-- @VA component alphabet = alphaNode
-- @VA component identity type = NodeID
-- @VA component active = false
-- @VA component process = Thread
-- @VA component alphabet = alphaThread
-- @VA component identity type = ThreadID
-- @VA component active = true
-- @VA serversRename = ServerSet
-- synchronisation set = SyncSet

-- A model of a lock-based stack, corresponding to the following pseudocode.
-- This version uses LPs in the obvious places.

-- This version has the watchdog synchronising with the thread before the node
-- is initiailised, and also on the setTop event, which aims to ensure all
-- nodes satisfy the expected invariant.

-- class Node(x: D, next: Node)
-- var Top: Node = null; val Lock = new Lock
-- def Push(x: D) = {
--   Lock.lock; val top = Top; val n = new Node(x, top); 
--   top = n; Lock.unlock
-- }
-- def Pop: D = {
--   Lock.lock; val top = Top
--   if(top == null){ Lock.unlock; return Empty }
--   else{ val n = top.next; Top = n; val x = n.x; 
--         Lock.unlock; return x }
-- }

include "definitions.csp"

-------- Basic types
-- the type of nodes; the former is for use with FDR; the latter with the
-- view abstraction program.
datatype NodeIDType =  Null | N0 | N1 | N2 | N3 | N4 | N5 | N6 
NodeID =  diff(NodeIDType, {Null})             -- real nodes
datatype Data = A | B | C                      -- the type of data
datatype ThreadID = T0 | T1 | T2 --| T3          -- the type of thread IDs

------------------------ Nodes ----------------------

channel initNode : ThreadID.NodeID.Data.NodeIDType -- initialise a node
channel getDatum : ThreadID.NodeID.Data -- read data from a node
channel getNext : ThreadID.NodeID.NodeIDType -- read next reference from a node
channel getPrev : ThreadID.NodeID.NodeIDType -- read prev reference from a node
channel setPrev : ThreadID.NodeID.NodeIDType -- add prev pointer

-- A single node with identity me, initially free
FreeNode(me) = initNode?t!me?datum?next -> Node(me, datum, next)

-- A node holding datum with reference to next
Node(me, datum, next) = 
  getDatum?t!me!datum -> Node(me, datum, next)
  [] getNext?t!me!next -> Node(me, datum, next)
  [] setPrev?t!me?prev -> DNode(me, datum, next, prev)

-- Now a node with a prev pointer
DNode(me, datum, next, prev) = 
  getDatum?t!me!datum -> DNode(me, datum, next, prev)
  [] getNext?t!me!next -> DNode(me, datum, next, prev)	
  [] getPrev?t!me!prev -> DNode(me, datum, next, prev)
  

alphaNode(n) =
  {| initNode.t.n, getDatum.t.n, getNext.t.n, getPrev.t.n, setPrev.t.n | 
       t <- ThreadID |}

Nodes = || n <- NodeID @ [alphaNode(n)] FreeNode(n)

---------------------- Servers ----------------------

-------- Variable storing the value of the top node

channel getTop : ThreadID.NodeIDType -- get the top pointer

-- Setting the top pointer is the LP for the relevant operation, as indicated
-- by a signal of the following type.

datatype Signal = PushOp.Data | PopOp.Data

channel setTop : ThreadID.NodeIDType.Signal

Top(top) = getTop?t!top -> Top(top) [] setTop?t?top1?_ -> Top(top1) 

alphaTop = {| getTop, setTop |}

-------- A lock process
channel lock, unlock : ThreadID
Lock = lock?t -> unlock.t -> Lock

alphaLock = {| lock, unlock |}

-------- Threads
channel push : ThreadID.Data   -- Signal operation
channel popEmpty : ThreadID         -- Signal operation

-- Following used in hacks to keep nodes in scope.  IMPROVE
-- channel dummy : ThreadID . NodeID

Pop(me) =
  lock.me ->  
  getTop.me?top ->
  if top != Null then
    getNext.me.top?next -> getDatum.me.top?d -> 
    setTop.me.next.PopOp.d ->  Unlock(me)
  else popEmpty.me -> Unlock(me)

Unlock(me) = unlock.me -> Thread(me)

Push(me) =
  lock.me -> getTop.me?top -> push.me?v:{A,B,C} -> initNode.me?node!v!top -> 
  setTop.me.node.PushOp.v -> Unlock(me)


-- Thread(me) = Pop(me)
Thread(me) = Push(me) [] Pop(me)

alphaThread(t) =
  {| initNode.t, getDatum.t, getNext.t, getTop.t, setTop.t, 
     lock.t, unlock.t, popEmpty.t, push.t |}

Threads = ||| t <- ThreadID @ Thread(t) 

-- -------- Watchdog.  Ensure that, once B is pushed: B is popped before A; that
-- -- B is popped only once; and that the stack is not empty before B is popped.
-- -- Note the alphabet of this process does not include push._.A or push._.C.

channel error

-- push1 events represent a thread choosing a data value to push.  push2 events
-- represent the value being added to the stack via a setTop event. 
channel push1, push2, pop1 : Data

WD0 =
  push1!A -> push2!A -> WD0
  [] push1!B -> push2!B -> WD1
  [] pop1?x -> (if x==A then WD0 else WDErr)
  [] popEmpty?_ -> WD0

-- Pushes from A*BC*; B not yet popped; expect more pushes of C; allow pops of
-- B or C.

WD1 =
  push1!C -> push2!C -> WD1
  [] pop1?x -> (if x==A then WDErr else if x==B then WD2 else WD1)
  [] popEmpty?_ -> WDErr

-- Pushes from A*BC*, B popped; expect more pushes of A; allow pops of A 
-- or empty.

WD2 =
  push1!A -> push2!A -> WD2
  [] pop1?x -> (if x==A then WD2 else WDErr)
  [] popEmpty?_ -> WD2

-- Signal error

WDErr = error -> STOP

WDRename = Union({
  {(push1.v, push.t.v) | v <- Data, t <- ThreadID },
  {(push2.v, setTop.t.n.PushOp.v) | v <- Data, t <- ThreadID, n <- NodeID},
  {(pop1.v, setTop.t.n.PopOp.v) | v <- Data, t <- ThreadID, n <- NodeIDType}
--   {(pop1.v, pop.t.v) | v <- Data, t <- ThreadID }
})

alphaWatchdog = Union({
  {| popEmpty, error, push, setTop |}
--  {setTop.t.n.PushOp.v | v <- Data, t <- ThreadID, n <- NodeID}
})

-------- Put servers together
ServerSet :: <(Proc, <(Event,Event)>, {Event}, Bool)>
ServerSet = 
  < (Lock, <>, alphaLock, false), (Top(Null), <>, alphaTop, false), 
    (WD0, seq(WDRename), alphaWatchdog, false) >

Servers = 
  || (S, R, Alpha, _) <- set(ServerSet) @ 
       [Alpha] S[[ e1 <- e2 | (e1,e2) <- set(R) ]]

SyncSet = {| setTop, getTop, lock, unlock, popEmpty, push |}

Components = (Nodes [| {| initNode,getNext,getDatum |} |] Threads)

System = Components [| SyncSet |] Servers
	 
-- The specification; Spec(s) represents a stack holding s

-- Spec(s) =
--   (if s != <> then pop!head(s) -> Spec(tail(s)) else popEmpty?t -> Spec(s))
--   [] length(s) <= card(NodeID) & push?d -> Spec(<d>^s)

-- System0 = 
--  (Components [| {| setTop, getTop, lock, unlock |} |] (Lock ||| Top(Null)))
--   [[ e1 <- e2 | (e2,e1) <- WDRename ]]
  
-- assert Spec(<>) [T= System0 \ diff(Events, {| push, pop, popEmpty |})

-- Watchdog-based specification

assert STOP [T= System \ diff(Events, {error})

assert System :[deadlock free]

assert System :[has trace [T]]: < 
  lock.T0, getTop.T0.Null, initNode.T0.N0.A.Null, 
  setTop.T0.N0.PushOp.A, unlock.T0,
  lock.T1, getTop.T1.N0, getNext.T1.N0.Null --, getDatum.T1.N0.A --,
  -- setTop.T1.Null.PopOp.A -- , unlock.T1 
>

assert System :[has trace [T]]: < 
  -- Push A, but with the node pointing to itself
  lock.T0, getTop.T0.Null, initNode.T0.N0.A.N0,
  setTop.T0.N0.PushOp.A, unlock.T0,
  -- Pop A
  lock.T1, getTop.T1.N0, getNext.T1.N0.N0, getDatum.T1.N0.A,
  setTop.T1.N0.PopOp.A, unlock.T1, 
  -- Pop A
  lock.T1, getTop.T1.N0, getNext.T1.N0.N0, getDatum.T1.N0.A,
  setTop.T1.N0.PopOp.A, unlock.T1 
>

assert STOP [T= STOP :[symmetry reduce]: -- NodeIDType